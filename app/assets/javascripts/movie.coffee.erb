$ ->
  sort_type = ""
  sort_by_title_direction = "desc"
  sort_by_primary_release_date_direction = "asc"

  data_params =
    api_key: "<%= ENV['tmdb_api_key']%>"
    include_adult: false
    include_video: false
    sort_by: ""
    without_genres: ""
    page: 1

  # return current sorting type
  getSortBy = ->
    if sort_type == 'original_title'
      return sort_type + '.' + sort_by_title_direction
    else if sort_type == 'primary_release_date'
      return sort_type + '.' + sort_by_primary_release_date_direction
    else
      return ""

  # return string of comma-separated genre ids to exclude from results
  getWithoutGenres = ->
    without_genres = ""
    $('input[type="checkbox"][name="genres[]"]').each ->
      if !this.checked
        without_genres += @value + ","
    return without_genres

  # update data params for discover query
  updateDataParams = ->
    data_params["without_genres"] = getWithoutGenres()
    data_params["sort_by"] = getSortBy()
    data_params["page"] = 1

  # return hash of movie details
  getMovieHash = (movie_item) ->
    movie = {}
    movie["title"] = movie_item["title"]
    movie["release_date"] = movie_item["release_date"]
    movie["genres"] = movie_item["genres"]
    movie["overview"] = movie_item["overview"]
    movie["poster_path"] = movie_item["poster_path"]
    movie["runtime"] = movie_item["runtime"]
    movie["id"] = movie_item["id"]
    return movie

  # display results in html given json of search results
  displayResults = (data) ->
    html = "";

    # render each movie result in html
    data["results"].forEach (movie_item) ->
      movie = getMovieHash(movie_item)
      movie_html = '<div class="movie-results-item">'
      unless movie_item['poster_path'] is null
        movie_html += '<div class="u-pull-left"><span>' + '<img src="http://image.tmdb.org/t/p/w185//' + movie['poster_path'] + '" class="movie-poster"></span></div>'
      movie_html += '<div class="u-pull-left"><h5><i><a href="/movies/' + movie['id'] + '" target="_blank">' + movie['title'] + '</a></i></h5>' + '<span><b>released on</b> ' + movie['release_date'] + '</span><br>' + '<span><a href="/movies/' + movie['id'] + '/reviews" target="_blank">view more</a></span> ' + '<span><a href="/movies/' + movie['id'] + '/reviews/new" target="_blank">leave a review</a></span>' + '</div><div class="u-cf"></div></div>'
      html += movie_html

    # add pagination
    html += '<div class="pagination">'
    if data["page"] isnt 1
      html += '<div class="u-pull-left"><a href="#" class="link-new-page" data-page=' + (data["page"] - 1) + '>← previous page</a></div>'
    if data["page"] isnt data["total_pages"]
      html += '<div class="u-pull-right"><a href="#" class="link-new-page" data-page=' + (data["page"] + 1) + '>next page →</a></div>'
    html += '<center>page ' + data["page"] + ' / ' + data["total_pages"] + '</center><div class="u-cf"></div></div>'

    # replace html with new results
    $('.movie-results').html html
    return

  # make ajax call to TMDB's discover api
  ajaxDiscoverCall = ->
    $.ajax
      type: 'GET'
      url: 'https://api.themoviedb.org/3/discover/movie'
      data: data_params
      beforeSend: (jqXHR, settings) ->
        console.log settings.url
      success: (data) ->
        displayResults(data)
        return
      error: (e) ->
        console.log e
        return

  # search based on filter params
  $('button[name="movies-search-form-submit"]').click ->
    updateDataParams()
    ajaxDiscoverCall()
    false


  # sort by title in ascending or descending order
  $('a.link-sort-title').click ->
    sort_type = 'original_title'

    # sort title by direction opposite of current direction
    if sort_by_title_direction == 'asc'
      sort_by_title_direction = 'desc'
      $('a.link-sort-title').text('title ▲')
      $('a.link-sort-release-date').text('release date')
    else
      sort_by_title_direction = 'asc'
      $('a.link-sort-title').text('title ▼')
      $('a.link-sort-release-date').text('release date')

    updateDataParams()
    ajaxDiscoverCall()
    false


  # sort by release date in ascending or descending order
  $('a.link-sort-release-date').click ->
    sort_type = 'primary_release_date'

    # sort release date by direction opposite of current direction
    if sort_by_primary_release_date_direction == 'asc'
      sort_by_primary_release_date_direction = 'desc'
      $('a.link-sort-release-date').text('release date ▼')
      $('a.link-sort-title').text('title')
    else
      sort_by_primary_release_date_direction = 'asc'
      $('a.link-sort-release-date').text('release date ▲')
      $('a.link-sort-title').text('title')

    updateDataParams()
    ajaxDiscoverCall()
    false

  # get previous or next page of results
  $('.movie-results').on 'click', 'a.link-new-page', ->
    data_params["page"] = $(this).data "page"
    ajaxDiscoverCall()
    false